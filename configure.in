##
## Configure template file for TuxNES
##
## Original code: The Mighty Mike Master <melanson@pcisys.net>
## BSD extensions by Jeroen Ruigrok van der Werven <asmodai@wxs.nl>
##
## $Id: configure.in,v 1.41 2001/04/16 01:08:07 tmmm Exp $
##

dnl --------------------------------------------------------------------
dnl Made the prerequisite autoconf 2.13, lower it to each version we
dnl find to work correctly with this script
dnl --------------------------------------------------------------------
AC_PREREQ(2.13)

dnl --------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script
dnl --------------------------------------------------------------------
AC_INIT(consts.h)
AM_INIT_AUTOMAKE(tuxnes,0.75)
#AM_INIT_AUTOMAKE(tuxnes,devel)

AM_CONFIG_HEADER(config.h)

dnl --------------------------------------------------------------------
dnl Get the canonical hostname which makes other tests easier
dnl --------------------------------------------------------------------
AC_CANONICAL_HOST

dnl --------------------------------------------------------------------
dnl Don't assume we want high level optimisations, it is known to
dnl produce broken code on certain architectures using certain compilers
dnl --------------------------------------------------------------------
CFLAGS="-O"
AC_SUBST(CFLAGS)

dnl --------------------------------------------------------------------
dnl Check for programs which we might need
dnl --------------------------------------------------------------------
AC_PROG_CC
AC_PROG_INSTALL

dnl --------------------------------------------------------------------
dnl Check whether user wants simple warnings or advanced warnings
dnl --------------------------------------------------------------------
AC_ARG_ENABLE(warnings, [  --enable-warnings       enable more compilation warning checks [default=no]], enable_warnings="yes", enable_warnings="no")

if test "x$enable_warnings" = "xyes"; then
  CFLAGS="$CFLAGS -pipe -W -Wall -ansi -pedantic -Wbad-function-cast \
  -Wcast-align -Wcast-qual -Wchar-subscripts -Wconversion -Winline \
  -Wmissing-prototypes -Wnested-externs -Wpointer-arith \
  -Wredundant-decls -Wshadow -Wstrict-prototypes -Wwrite-strings"
else
  CFLAGS="$CFLAGS -pipe -Wall"
fi
AC_SUBST(CFLAGS)

dnl --------------------------------------------------------------------
dnl Check whether user wants to enable profiling support
dnl --------------------------------------------------------------------
AC_ARG_ENABLE(profiling, [  --enable-profiling      enable profiling of functions [default=no]], enable_profiling="yes", enable_profiling="no")

if test "x$enable_profiling" = "xyes"; then
  CFLAGS="$CFLAGS -pg"
else
  CFLAGS="$CFLAGS"
fi
AC_SUBST(CFLAGS)

dnl --------------------------------------------------------------------
dnl Checks for libraries
dnl --------------------------------------------------------------------

dnl --------------------------------------------------------------------
dnl Checks for header files
dnl --------------------------------------------------------------------
AC_PATH_XTRA
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(features.h fcntl.h strings.h sys/time.h \
                 linux/joystick.h linux/soundcard.h sys/soundcard.h \
                 ppm.h)
AC_CHECK_HEADERS(snss.h)

dnl --------------------------------------------------------------------
dnl Checks for X11 header files
dnl --------------------------------------------------------------------
if test x"$no_x" != xyes
then
  AC_CHECK_HEADERS(X11/xpm.h X11/vroot.h \
                   sys/ipc.h sys/shm.h X11/extensions/XShm.h \
                   X11/extensions/Xext.h)
fi

dnl --------------------------------------------------------------------
dnl Check for the getopt.h header file, Linux by default has it, but we
dnl want to exit configure when we do not have it on FreeBSD
dnl --------------------------------------------------------------------

case "$host" in
  *-freebsd*)
  OCPPFLAGS="$CPPFLAGS"
  CPPFLAGS="-I/usr/local/include"
  AC_CHECK_HEADER(getopt.h, CFLAGS="$CFLAGS -I/usr/local/include", AC_MSG_ERROR(Can't find getopt.h.))
  CPPFLAGS="$OCPPFLAGS";;
esac
	
dnl --------------------------------------------------------------------
dnl Checks for X11-dependant libraries
dnl --------------------------------------------------------------------

if test x"$no_x" != xyes
then
  LIBS="$LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS $X_LIBS"
  CFLAGS="$CFLAGS $X_CFLAGS"
  AC_CHECK_LIB(Xpm, XpmWriteFileFromImage)
  AC_CHECK_LIB(Xext, XMissingExtension)
fi

dnl --------------------------------------------------------------------
dnl Checks for possibly-X11 dependant libraries
dnl --------------------------------------------------------------------

AC_CHECK_LIB(z, gzgetc)
AC_CHECK_LIB(pbm, pbm_writepbm)
AC_CHECK_LIB(pgm, pgm_writepgm,,, -lpbm)
AC_CHECK_LIB(ppm, ppm_writeppm,,, -lpgm -lpbm)
AC_CHECK_LIB(m, sin)
case "$host" in
  *)
  OCPPFLAGS="$CPPFLAGS"
  CPPFLAGS="-I/usr/local/include"
  OLDFLAGS="$LDFLAGS"
  LDFLAGS="-L/usr/local/lib"
  CPPFLAGS="$OCPPFLAGS"
  LDFLAGS="$OLDFLAGS";;
esac
AC_CHECK_LIB(snss, openSnssFile)

dnl --------------------------------------------------------------------
dnl Check for GGI
dnl --------------------------------------------------------------------
AC_ARG_WITH(ggi,\
[  --without-ggi           disable check for GGI (General Graphics Interface)])

if test "x$with_ggi" != "xno"; then
  OCPPFLAGS="$CPPFLAGS"
  CPPFLAGS="-I/usr/local/include"
  AC_CHECK_HEADERS(ggi/gii.h ggi/ggi.h, CFLAGS="$CFLAGS -I/usr/local/include")
  AC_CHECK_LIB(ggi, ggiInit)
  AC_CHECK_LIB(gii, giiInit)
  CPPFLAGS="$OCPPFLAGS"
fi

dnl --------------------------------------------------------------------
dnl Check for the W Window System
dnl --------------------------------------------------------------------
AC_ARG_WITH(w,\
[  --without-w             disable check for the W Window System])

if test "x$with_w" != "xno"; then
  OCPPFLAGS="$CPPFLAGS"
  CPPFLAGS="-I/usr/local/include"
  AC_CHECK_HEADERS(Wlib.h, CFLAGS="$CFLAGS -I/usr/local/include")
  AC_CHECK_LIB(W, w_init)
  CPPFLAGS="$OCPPFLAGS"
fi

dnl --------------------------------------------------------------------
dnl Check for the gnugetopt library, this is to make sure we can use the
dnl getopt support under FreeBSD
dnl --------------------------------------------------------------------

case "$host" in
  *-freebsd*)
  OLDFLAGS="$LDFLAGS"
  LDFLAGS="-L/usr/local/lib"
  AC_CHECK_LIB(gnugetopt, getopt, [AC_DEFINE(HAVE_LIBGNUGETOPT) GNUGETOPT_LIB="-lgnugetopt"])
  if test -n "$GNUGETOPT_LIB"; then
  	LDFLAGS="$OLDFLAGS -L/usr/local/lib -lgnugetopt"
	AC_SUBST(LDFLAGS)
  else
	LDFLAGS="$OLDFLAGS"
  fi;;
esac

dnl --------------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics.
dnl Due to differences in defines we add the -D__USE_BSD to Linux
dnl compilation since every supported OS has the non-prefixed
dnl definitions, and we don't need to worry about namespace pollution.
dnl --------------------------------------------------------------------
AC_C_BIGENDIAN
case "$host" in
  *-linux*)
  CFLAGS="$CFLAGS -D__USE_BSD";;
esac
AC_SUBST(CFLAGS)
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME
AC_TYPE_SIZE_T

dnl --------------------------------------------------------------------
dnl Checks for library functions.
dnl --------------------------------------------------------------------
AC_FUNC_MEMCMP
AC_FUNC_MMAP
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(gettimeofday strtod strtoul)

AC_OUTPUT(Makefile)
